// Top-level build file where you can add configuration options common to all sub-projects/modules.

allprojects {
	repositories {
		mavenCentral()
		mavenLocal()
		google()
		maven { url "https://jitpack.io" }
	}
	afterEvaluate {
		tasks.withType(Test) {
			// Allow tests to be re-run if any optional tests are enabled
			outputs.upToDateWhen { System.getenv("OPTIONAL_TESTS") == null }
			// Use entropy-gathering device specified on command line, if any
			systemProperty 'java.security.egd', System.getProperty('java.security.egd')
		}
	}
}

buildscript {
	repositories {
		mavenLocal()
		google()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}

	ext {
		kotlin_version = '1.7.10'
		dagger_version = "2.43.2"
		// okhttp 3.12.x is supported until end of 2021, newer versions need minSdk 21
		okhttp_version = "3.12.13"
		jackson_version = "2.13.4"
		tor_version = "0.4.5.14"
		obfs4proxy_version = "0.0.14-tor1"
		snowflake_version = "2.3.1"
		junit_version = "4.13.2"
		jmock_version = '2.12.0'
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:7.2.2'
		classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.5.3'
		classpath files('libs/gradle-witness.jar')
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

if ((project.hasProperty("briar.mailbox_integration_tests") && project.property("briar.mailbox_integration_tests") == "true")
		|| System.env.MAILBOX_INTEGRATION_TESTS) {
	configure([project(':mailbox-core'), project(':mailbox-lib')]) {
		apply from: "../gradle/variables.gradle"
	}
}
